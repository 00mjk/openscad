greaterThan(QT_MAJOR_VERSION, 4) {
    QT += widgets printsupport

    greaterThan(QT_MINOR_VERSION, 1) {
        macx:QT += macextras
    }
}

INCLUDEPATH += $$[QT_INSTALL_HEADERS]

LIBS += -L$$[QT_INSTALL_LIBS]

QT5LIB=qt5scintilla2

unix:linux* {
    FEDORA32LIBS=/usr/lib/libqscintilla2-qt5.so
    FEDORA64LIBS=/usr/lib64/libqscintilla2-qt5.so
    exists($$FEDORA32LIBS) | exists($$FEDORA64LIBS) {
        QT5LIB=qscintilla2-qt5
    }
}

exists($$OPENSCAD_LIBDIR/lib/libqscintilla2.so) {
    greaterThan(QT_MAJOR_VERSION, 4) {
        QT5LIB=qscintilla2
    }
}

CONFIG(debug, debug|release) {
    mac: {
	#LIBS += -lqscintilla2_debug
	LIBS += -lqscintilla2
    } else {
	win32: {
            # for DEBUG build on MSYS2, check that we actually have debug
            # version of qscintilla2 available. note the \msys64 prefix
            # and \ slashes (/ wont work)
            _MSYSTEM = $$(MSYSTEM)
            contains( _MSYSTEM, MINGW64 ) {
               dblib = \msys64\mingw64\bin\qscintilla2d.dll
               !exists( $$dblib ) {
                  error("cant find $$dblib (debug lib)")
               }
            }
            contains( _MSYSTEM, MINGW32 ) {
               dblib = \msys64\mingw32\bin\qscintilla2d.dll
               !exists( $$dblib ) {
                  error("cant find $$dblib (debug lib)")
               }
            }
	    LIBS += -lqscintilla2d
	} else {
	    greaterThan(QT_MAJOR_VERSION, 4) {
		message("Using $$QT5LIB as library name")
		LIBS += -l$$QT5LIB
	    } else {
		LIBS += -lqscintilla2
	    }
	}
    }
} else {
    mac: {
	LIBS += -lqscintilla2
    } else {
      win32: {
        LIBS += -lqscintilla2
      } else {
        greaterThan(QT_MAJOR_VERSION, 4) {
	    message("Using $$QT5LIB as library name")
	    LIBS += -l$$QT5LIB
        } else {
	    LIBS += -lqscintilla2
        }
      }
    }
}
