# @OSCAD_TEMPLATE_HEADER@
#
# Template to build a CTestCustom.cmake file in the build directory.
#
# This template enables ctest to do things before and after the test runs.
# In our case, 1. Setup a virtual framebuffer, 2. pretty-print results
# 
# As a template, this file is read inside of CMakeList.txt and certain
# words (@word@) are replaced with the cmake build dir/etc, at the time that
# cmake itself is run.

# Part 0. Finding MCAD

set(ENV{OPENSCADPATH} "@MCAD_HOST_DIR@")

# Part 1. X11 and Virtual Framebuffer, for headless Linux/BSD systems. 
# This is not used by Windows or Mac because they have alternate means 
# of accessing OpenGL.

set(debug_openscad_template 1)

if( @CMAKE_SYSTEM_NAME@ MATCHES "Linux|BSD")
 set(DISPLAY_ENV $ENV{DISPLAY})
 if(DISPLAY_ENV)
  message("X11 DISPLAY environment variable found.")
  message("Assuming framebuffer exists at DISPLAY=${DISPLAY_ENV}")
 else()
  message("X11 DISPLAY environment variable not found. Calling virtualfb.sh")
  execute_process(
   COMMAND @TESTTOOLS_HOST_DIR@/virtualfb.sh OUTPUT_VARIABLE SVFB_OUT)
  string(REGEX MATCH "DISPLAY=:[0-9.]*" VFB_DISPLAY_STR "${SVFB_OUT}")
  string(REGEX MATCH ":[0-9.]*" VFB_DISPLAY "${VFB_DISPLAY_STR}")
  string(REGEX MATCH "PID=[0-9.]*" VFB_PID_STR "${SVFB_OUT}")
  string(REPLACE "PID=" "" VFB_PID "${VFB_PID_STR}")

  if (debug_openscad_template)
    message("Output from virtualfb.sh script: ${SVFB_OUT}")
    message("Display: ${VFB_DISPLAY}")
    message("Process ID of vfb: ${VFB_PID}")
  endif()
  if ("${VFB_DISPLAY}" STREQUAL "" OR "${VFB_PID}" STREQUAL "")
    set(VFBLOG "virtualfb.log")
    message("Virtual framebuffer had a problem starting. Printing ${VFBLOG}")
    execute_process(COMMAND cat virtualfb.log OUTPUT_VARIABLE VFBLOGTXT)
    message("Log: ${VFBLOGTXT}")
  else()
    message("Virtual framebuffer started. DISPLAY=${VFB_DISPLAY}, PID=${VFB_PID}")
  endif()


  # This line is the heart of the mechanism by which ctest is able to take 
  # the DISPLAY env variable of the Virtual Framebuffer it just started, and 
  # pass it on to its child processes (the tests from CTestTestfile.cmake 
  # in the build directory).
  set(ENV{DISPLAY} "${VFB_DISPLAY}")

  set(CTEST_CUSTOM_POST_TEST "@TESTTOOLS_HOST_DIR@/virtualfb.sh")
 endif()
endif()

# Part 2. Pretty Printing

message("running '@OPENSCAD_HOST_BINARY@ --info' to generate sysinfo.txt")
execute_process(COMMAND @OPENSCAD_HOST_BINARY@ --info OUTPUT_FILE sysinfo.txt)

set(CTEST_CUSTOM_POST_TEST ${CTEST_CUSTOM_POST_TEST} "@PYTHON_HOST_BINARY@ \"@TESTTOOLS_HOST_DIR@/test_pretty_print.py\" @UPLOADARG@")

if ( ${debug_openscad_template} )
  foreach(post_test ${CTEST_CUSTOM_POST_TEST} )
    message("Post test:" ${post_test})
  endforeach()
endif()
